/// Rellenar en un subproceso una matriz cuadrada con números aleatorios salvo en la diagonal 
/// principal, la cual debe rellenarse con ceros. Una vez llena la matriz debe generar otro 
/// subproceso para imprimir la matriz.

Algoritmo guia_arreglosej4
	
	Definir i, j, n Como Entero
	Definir matriz Como Real
	Escribir "************************************************************************";
	Escribir"Bienvenido, ingrese el número de filas/columnas para una matriz cuadrada: ";
	Leer n;
	
	Dimension matriz(n,n);
	llenaMatriz(matriz,n);
	mostrarMatriz(matriz,n);
	
FinAlgoritmo

SubProceso llenaMatriz(matriz,n)
	Definir i, j Como Entero
	Para i = 0 Hasta n-1 Con Paso 1 Hacer
		Para j = 0 hasta n-1 Con Paso 1 hacer
			Si i = j Entonces
				matriz(i,j) = 0;
			SiNo
				matriz(i,j) = Aleatorio(1,10);
			FinSi
		FinPara
	FinPara
FinSubProceso

SubProceso mostrarMatriz(matriz,n)
	Definir i, j Como Entero
	Para i = 0 Hasta n-1 Con Paso 1 Hacer
		Para j = 0 Hasta n-1 Con Paso 1 Hacer
			Escribir matriz(i,j), "  " Sin Saltar
		FinPara
		Escribir " ";
	FinPara
FinSubProceso
